 # Copyright (C) 2022-2023, Advanced Micro Devices. All rights reserved.
 #
 # Redistribution and use in source and binary forms, with or without
 # modification, are permitted provided that the following conditions are met:
 # 1. Redistributions of source code must retain the above copyright notice,
 #    this list of conditions and the following disclaimer.
 # 2. Redistributions in binary form must reproduce the above copyright notice,
 #    this list of conditions and the following disclaimer in the documentation
 #    and/or other materials provided with the distribution.
 # 3. Neither the name of the copyright holder nor the names of its contributors
 #    may be used to endorse or promote products derived from this software
 # without specific prior written permission.
 #
 # THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 # AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 # IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 # ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 # LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 # CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 # SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 # INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 # CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 # ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 # POSSIBILITY OF SUCH DAMAGE.

include(TestBigEndian)

CMAKE_MINIMUM_REQUIRED(VERSION 3.1)

SET(CMAKE_MODULE_PATH
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake"
  # Add default path
  ${CMAKE_MODULE_PATH}
  )

INCLUDE(AlcpUtils)
INCLUDE(AlcpConfig)

# OS Specific includes
IF(UNIX)
  INCLUDE(CompilerLinux)
ELSEIF(WIN32)
  INCLUDE(CompilerWin)
ENDIF(UNIX)

PROJECT (ALCP)

#FIXME: is this to be made common for win and lin?
alcp_check_cmake_version()

# check for compiler version
alcp_check_compiler_version()

# SPECIFY THE C STANDARD
SET(CMAKE_C_STANDARD 11)
SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_C_STANDARD_REQUIRED TRUE)
SET(CMAKE_CXX_STANDARD_REQUIRED TRUE)

SET(CMAKE_CXX_FLAGS_RELEASE "-DNDEBUG")

IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE RELEASE) 
ENDIF()

IF(ALCP_ENABLE_CLANG_TIDY)
	IF(NOT CMAKE_CXX_CLANG_TIDY)
		FIND_PROGRAM(CLANG_TIDY "clang-tidy-12")
		SET(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY}")
	ENDIF(NOT CMAKE_CXX_CLANG_TIDY)
ENDIF(ALCP_ENABLE_CLANG_TIDY)

SET( CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake" )

# Make the static and shared libraries are built in top most directory
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")

# We want examples and other tests to be in their respective folder,
# Hence this is commented out
#SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")

# All the options which are avaiable with ALCP
# These options can be listed with cmake -LH
OPTION(ALCP_ENABLE_DOCS "ENABLE DOCUMENTATION" OFF)
OPTION(ALCP_ENABLE_DOXYGEN "ENABLE DOXYGEN DOCUMENTATION GENERATION" OFF)
OPTION(ALCP_ENABLE_CLANG_TIDY "ENABLE CLANG TIDY" OFF)
OPTION(ALCP_ENABLE_TESTS "ENABLE TESTING" OFF)
OPTION(ALCP_ENABLE_BENCH "ENABLE BENCHMARKING" OFF)
OPTION(ALCP_ENABLE_EXAMPLES "ENABLE EXAMPLES" OFF)
set(AOCL_RELEASE_VERSION "4.0.1" CACHE STRING "AOCL RELEASE VERSION")
OPTION(ENABLE_AOCL_CPUID "ENABLE SUPPORT FOR CPUID BASED DISPATCHING" OFF)
set(AOCL_CPUID_INSTALL_DIR "" CACHE STRING "AOCL CPUID INSTALLED DIRECTORY")
set(AOCL_COMPAT_LIBS "" CACHE STRING "COMPATIBILITY LAYER FOR OPENSSL AND IPP")

# Testing/Benchmarking options
OPTION(ENABLE_TESTS_IPP_API "ENABLE IPP CALLS IN TESTING AND BENCHMARKING" OFF)
OPTION(ENABLE_TESTS_OPENSSL_API "ENABLE OPENSSL CALLS IN TESTING AND BENCHMARKING" OFF)
set(OPENSSL_INSTALL_DIR "" CACHE STRING "OPENSSL INSTALLED DIRECTORY FOR TESTING AND BENCHMARKING")
set(IPP_INSTALL_DIR "" CACHE STRING "IPPCP INSTALLED DIRECTORY FOR TESTING AND BENCHMARKING")

# misc options
OPTION(ALCP_USE_ASAN "COMPILE WITH address sanitizer SUPPORT TO DETECT MEMORY ERRORS" OFF)
if(ALCP_USE_ASAN)
    alcp_add_asan_flags()
endif()

OPTION(ALCP_USE_TSAN "COMPILE WITH Thread Sanitizer SUPPORT TO DETECT DATA RACE ERRORS" OFF)
if(ALCP_USE_TSAN)
    alcp_add_tsan_flags()
endif()

OPTION(ALCP_USE_UBSAN "COMPILE WITH UB Sanitizer SUPPORT TO DETECT undefined behavior" OFF)
if(ALCP_USE_UBSAN)
    alcp_add_ubsan_flags()
endif()

IF (NOT DEFINED ALCP_BIGNUM_USE_OPENSSL)
	SET (ALCP_BIGNUM_USE_OPENSSL ON)
ENDIF()

# Generate Configuration config.h
GEN_CONF()
if (CMAKE_BUILD_TYPE STREQUAL "Coverage")
    alcp_add_coverage_flags()
endif()

IF (NOT DEFINED BUILD_SHARED_LIBS)
	SET (BUILD_SHARED_LIBS ON)
ENDIF()

SET(CMAKE_DEBUG_POSTFIX _DEBUG)

ADD_SUBDIRECTORY(lib)

IF (ALCP_ENABLE_DOCS)
	ADD_SUBDIRECTORY(docs)
ENDIF()

IF(ALCP_ENABLE_DOXYGEN)
	include(${CMAKE_SOURCE_DIR}/cmake/doxygen.cmake)
ENDIF(ALCP_ENABLE_DOXYGEN)

# building examples
IF (ALCP_ENABLE_EXAMPLES)
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/examples)
		MESSAGE(FATAL_ERROR "Examples dir not found!")
        ENDIF()
	ADD_SUBDIRECTORY(examples)
	MESSAGE(STATUS "Building examples")
ENDIF()

# building tests
IF (ALCP_ENABLE_TESTS)
	ENABLE_TESTING()
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/tests)
    	MESSAGE(FATAL_ERROR "Tests folder not found!")
	ENDIF()
	ADD_SUBDIRECTORY(tests)
  MESSAGE(STATUS "Enabling Tests")
ENDIF()

# building test bench
IF (ALCP_ENABLE_BENCH)
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/bench)
    	MESSAGE(FATAL_ERROR "Bench folder not found!")
    ENDIF()
	ADD_SUBDIRECTORY(bench)
	MESSAGE(STATUS "Enabling Benchmarks")
ENDIF()

#INSTALL(TARGETS ALCP
#	RUNTIME DESTINATION BIN
#	LIBRARY DESTINATION LIB
#	ARCHIVE DESTINATION LIB)

#INSTALL(DIRECTORY INCLUDE/API
#	RENAME DESTINATION INCLUDE/ALCP)

INSTALL(TARGETS alcp alcp_static)

INSTALL(DIRECTORY ${CMAKE_SOURCE_DIR}/include/alcp
        TYPE INCLUDE
		)

IF (ALCP_ENABLE_EXAMPLES)
	FILE(GLOB EXAMPLES_FILES_CIPHER
			${CMAKE_CURRENT_SOURCE_DIR}/examples/cipher/*.c
	#		${CMAKE_CURRENT_SOURCE_DIR}/examples/cipher/*.cc
			)
	FILE(GLOB EXAMPLES_FILES_DIGEST
			${CMAKE_CURRENT_SOURCE_DIR}/examples/digest/*.c
			)
	FILE(GLOB EXAMPLES_FILES_RNG
			${CMAKE_CURRENT_SOURCE_DIR}/examples/rng/*.c
			)
	FILE(GLOB EXAMPLES_FILES_MISC
			${CMAKE_CURRENT_SOURCE_DIR}/examples/misc/*.c
			)

	INSTALL(FILES ${EXAMPLES_FILES_CIPHER}
			DESTINATION examples/cipher
			)
	INSTALL(FILES ${EXAMPLES_FILES_DIGEST}
			DESTINATION examples/digest
			)
	INSTALL(FILES ${EXAMPLES_FILES_RNG}
			DESTINATION examples/rng
			)
	INSTALL(FILES ${EXAMPLES_FILES_MISC} 
			DESTINATION examples/misc
			)
	INSTALL(FILES examples/Makefile
			DESTINATION .)
ENDIF (ALCP_ENABLE_EXAMPLES)

# print configure summary
MESSAGE(STATUS "\n--------------------Configure Summary--------------------\n")

# function to filter and print variables based on wildcard
FUNCTION (GetVars _prefix)
    GET_CMAKE_PROPERTY(_vars VARIABLES)
    STRING (REGEX MATCHALL "(^|)${_prefix}[A-Za-z0-9_]*" matches "${_vars}")
	FOREACH (var ${matches})
        IF (${var})
			MESSAGE("${var}=${${var}}")
	 	ENDIF()
	ENDFOREACH()
ENDFUNCTION()

# print cmake vars
MESSAGE ("CMAKE version: ${CMAKE_VERSION}")
MESSAGE ("CMAKE Compiler ID: ${CMAKE_CXX_COMPILER_ID}")
MESSAGE ("CMAKE C COMPILER: ${CMAKE_C_COMPILER}")
MESSAGE ("CMAKE CXX COMPILER: ${CMAKE_CXX_COMPILER}")
MESSAGE ("CMAKE C Compiler version: ${CMAKE_C_COMPILER_VERSION}")
MESSAGE ("CMAKE CXX Compiler version: ${CMAKE_CXX_COMPILER_VERSION}")
MESSAGE ("CMAKE CXX Flags Release: ${CMAKE_CXX_FLAGS_RELEASE}")
MESSAGE ("CMAKE CXX Flags Debug: ${CMAKE_CXX_FLAGS_DEBUG}")
MESSAGE ("CMAKE CXX Flags: ${CMAKE_CXX_FLAGS}")
MESSAGE ("CMAKE BUILD TYPE: ${CMAKE_BUILD_TYPE}")
MESSAGE ("CMAKE CXX STANDARD: ${CMAKE_CXX_STANDARD}")

# alcp vars
# FIXME: better to have all the below arguments starting with alcp_*, 
GetVars("ALCP_")
GetVars("AOCL_")
GetVars("IPP_")
GetVars("OPENSSL_")

# FixMe:project name and version to be printed..
