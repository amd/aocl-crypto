CMAKE_MINIMUM_REQUIRED(VERSION 3.1)
PROJECT (ALCP)

IF(${CMAKE_VERSION} VERSION_LESS "3.1.0")
    MESSAGE(FATAL_ERROR "Minimum reqd version is CMake 3.1.0!")
ENDIF()

# SPECIFY THE C STANDARD
SET(CMAKE_C_STANDARD 11)
SET(CMAKE_CXX_STANDARD 17)
SET(CMAKE_C_STANDARD_REQUIRED TRUE)
SET(CMAKE_CXX_STANDARD_REQUIRED TRUE)

IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE RELEASE) 
ENDIF()

SET( CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake" )

# Make the static and shared libraries are built in top most directory
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")

# We want examples and other tests to be in their respective folder,
# Hence this is commented out
#SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_BINARY_DIR}")

# All the options which are avaiable with ALCP
# These options can be listed with cmake -LH
OPTION(ALCP_ENABLE_DOCS "ENABLE DOCUMENTATION" OFF)
OPTION(ALCP_ENABLE_TESTS "ENABLE TESTING" OFF)
OPTION(ALCP_ENABLE_BENCH "ENABLE BENCHMARKING" OFF)
OPTION(ALCP_ENABLE_EXAMPLES "ENABLE EXAMPLES" OFF)
set(AOCL_RELEASE_VERSION "3.2.1" CACHE STRING "AOCL RELEASE VERSION")
OPTION(ENABLE_AOCL_CPUID "ENABLE SUPPORT FOR CPUID BASED DISPATCHING" OFF)
set(AOCL_CPUID_INSTALL_DIR "" CACHE STRING "AOCL CPUID INSTALLED DIRECTORY")
set(AOCL_COMPAT_LIBS "" CACHE STRING "COMPATIBILITY LAYER FOR OPENSSL AND IPP")

# Testing/Benchmarking options
OPTION(ENABLE_TESTS_IPP_API "ENABLE IPP CALLS IN TESTING AND BENCHMARKING" OFF)
OPTION(ENABLE_TESTS_OPENSSL_API "ENABLE OPENSSL CALLS IN TESTING AND BENCHMARKING" OFF)
set(OPENSSL_INSTALL_DIR "" CACHE STRING "OPENSSL INSTALLED DIRECTORY FOR TESTING AND BENCHMARKING")
set(IPP_INSTALL_DIR "" CACHE STRING "IPPCP INSTALLED DIRECTORY FOR TESTING AND BENCHMARKING")




IF (NOT DEFINED BUILD_SHARED_LIBS)
	SET (BUILD_SHARED_LIBS ON)
ENDIF()

SET(CMAKE_DEBUG_POSTFIX _DEBUG)


# CONFIGURE A HEADER FILE TO PASS SOME OF THE CMAKE SETTINGS
# TO THE SOURCE CODE
CONFIGURE_FILE(include/alcp/config.h.in include/alcp/config.h)

ADD_SUBDIRECTORY(lib)

IF (ALCP_ENABLE_DOCS)
	ADD_SUBDIRECTORY(docs)
ENDIF()

# building examples
IF (ALCP_ENABLE_EXAMPLES)
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/examples)
		MESSAGE(FATAL_ERROR "Examples dir not found!")
        ENDIF()
	ADD_SUBDIRECTORY(examples)
	MESSAGE(STATUS "Building examples")
ENDIF()

# building tests
IF (ALCP_ENABLE_TESTS)
	ENABLE_TESTING()
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/tests)
    	MESSAGE(FATAL_ERROR "Tests folder not found!")
	ENDIF()
	ADD_SUBDIRECTORY(tests)
  MESSAGE(STATUS "Enabling Tests")
ENDIF()

# building test bench
IF (ALCP_ENABLE_BENCH)
	IF (NOT EXISTS ${CMAKE_CURRENT_LIST_DIR}/bench)
    	MESSAGE(FATAL_ERROR "Bench folder not found!")
    ENDIF()
	ADD_SUBDIRECTORY(bench)
	MESSAGE(STATUS "Enabling Benchmarks")
ENDIF()

#INSTALL(TARGETS ALCP
#	RUNTIME DESTINATION BIN
#	LIBRARY DESTINATION LIB
#	ARCHIVE DESTINATION LIB)

#INSTALL(DIRECTORY INCLUDE/API
#	RENAME DESTINATION INCLUDE/ALCP)

INSTALL(TARGETS alcp alcp_static)

INSTALL(DIRECTORY ${CMAKE_SOURCE_DIR}/include/alcp
        TYPE INCLUDE
		)

IF (ALCP_ENABLE_EXAMPLES)
	FILE(GLOB EXAMPLES_FILES_CIPHER
			${CMAKE_CURRENT_SOURCE_DIR}/examples/cipher/*.c
	#		${CMAKE_CURRENT_SOURCE_DIR}/examples/cipher/*.cc
			)
	FILE(GLOB EXAMPLES_FILES_DIGEST
			${CMAKE_CURRENT_SOURCE_DIR}/examples/digest/*.c
			)
	FILE(GLOB EXAMPLES_FILES_RNG
			${CMAKE_CURRENT_SOURCE_DIR}/examples/rng/*.c
			)
	FILE(GLOB EXAMPLES_FILES_MISC
			${CMAKE_CURRENT_SOURCE_DIR}/examples/misc/*.c
			)

	INSTALL(FILES ${EXAMPLES_FILES_CIPHER}
			DESTINATION examples/cipher
			)
	INSTALL(FILES ${EXAMPLES_FILES_DIGEST}
			DESTINATION examples/digest
			)
	INSTALL(FILES ${EXAMPLES_FILES_RNG}
			DESTINATION examples/rng
			)
	INSTALL(FILES ${EXAMPLES_FILES_MISC} 
			DESTINATION examples/misc
			)
	INSTALL(FILES examples/Makefile
			DESTINATION .)
ENDIF (ALCP_ENABLE_EXAMPLES)

# print configure summary
MESSAGE(STATUS "\n--------------------Configure Summary--------------------\n")

# function to filter and print variables based on wildcard
FUNCTION (GetVars _prefix)
    GET_CMAKE_PROPERTY(_vars VARIABLES)
    STRING (REGEX MATCHALL "(^|)${_prefix}[A-Za-z0-9_]*" matches "${_vars}")
	FOREACH (var ${matches})
        IF (${var})
			MESSAGE("${var}=${${var}}")
	 	ENDIF()
	ENDFOREACH()
ENDFUNCTION()

# print cmake vars
MESSAGE ("CMAKE version: ${CMAKE_VERSION}")
MESSAGE ("CMAKE Compiler ID: ${CMAKE_CXX_COMPILER_ID}")
MESSAGE ("CMAKE C COMPILER: ${CMAKE_C_COMPILER}")
MESSAGE ("CMAKE CXX COMPILER: ${CMAKE_CXX_COMPILER}")
MESSAGE ("CMAKE C Compiler version: ${CMAKE_C_COMPILER_VERSION}")
MESSAGE ("CMAKE CXX Compiler version: ${CMAKE_CXX_COMPILER_VERSION}")
MESSAGE ("CMAKE BUILD TYPE: ${CMAKE_BUILD_TYPE}")

# alcp vars
# FIXME: better to have all the below arguments starting with alcp_*, 
GetVars("ALCP_")
GetVars("AOCL_")
GetVars("IPP_")
GetVars("OPENSSL_")

# FixMe:project name and version to be printed..

