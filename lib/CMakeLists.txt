 # Copyright (C) 2021-2023, Advanced Micro Devices. All rights reserved.
 #
 # Redistribution and use in source and binary forms, with or without
 # modification, are permitted provided that the following conditions are met:
 # 1. Redistributions of source code must retain the above copyright notice,
 #    this list of conditions and the following disclaimer.
 # 2. Redistributions in binary form must reproduce the above copyright notice,
 #    this list of conditions and the following disclaimer in the documentation
 #    and/or other materials provided with the distribution.
 # 3. Neither the name of the copyright holder nor the names of its contributors
 #    may be used to endorse or promote products derived from this software
 # without specific prior written permission.
 #
 # THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 # AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 # IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 # ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 # LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 # CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 # SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 # INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 # CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 # ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 # POSSIBILITY OF SUCH DAMAGE.


#FILE(GLOB COMMON_SRCS "*.cc" )

#
# The Policy will throw error about TARGET_SOURCES makes the
# relative paths as absolute
#

IF(POLICY CMP0076)
    CMAKE_POLICY(SET CMP0076 NEW)
ENDIF()

IF(POLICY CMP0079)
    CMAKE_POLICY(SET CMP0079 NEW)
ENDIF()


SET(ALCP_VRS_SRC
	version.cc)

SET(ALCP_COMMON_SRCS
	dynlib.cc
	error.cc
	exception.cc
	module.cc
	modulemanager.cc
	sourcelocation.cc
  status.cc
  bufferwriter.cc
  padding.cc
	)

#[==[
# add_compile_options_config(<CONFIG> <option> ...)
#FUNCTION(ADD_COMPILE_OPTIONS_CONFIG CONFIG)
#    FOREACH(opt ${ARGN})
#        ADD_COMPILE_OPTIONS("$<$<CONFIG:${CONFIG}>:${opt}>")
#    ENDFOREACH()
#ENDFUNCTION()
#]==]

INCLUDE(AlcpUtils)

# Get Compiler Flags, defined in cmake/Compiler*.cmake
alcp_get_cflags_warnings()
alcp_get_cflags()
alcp_get_cflags_debug()
alcp_get_cflags_arch()

IF (MSVC AND NOT CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
	ADD_COMPILE_OPTIONS(/W3 /WX)
ELSEIF (CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
	ADD_COMPILE_OPTIONS_CONFIG(RELEASE ${ALCP_CFLAGS} ${ALCP_CFLAGS_WARNINGS})
	ADD_COMPILE_OPTIONS_CONFIG(DEBUG ${ALCP_CFLAGS_DEBUG} ${ALCP_CFLAGS_WARNINGS})
	ADD_COMPILE_OPTIONS(${ALCP_CFLAGS_ARCH})
ELSE ()
	ADD_COMPILE_OPTIONS_CONFIG(RELEASE ${ALCP_CFLAGS} ${ALCP_CFLAGS_WARNINGS})
	ADD_COMPILE_OPTIONS_CONFIG(DEBUG ${ALCP_CFLAGS_DEBUG} ${ALCP_CFLAGS_WARNINGS})
ENDIF()

INCLUDE_DIRECTORIES(
	${CMAKE_SOURCE_DIR}/include
	)

SET(ALCP_LIB_SOURCES
	${ALCP_VRS_SRC}
	${ALCP_COMMON_SRCS}
	)

ADD_LIBRARY(alcp SHARED
	${ALCP_LIB_SOURCES}
	)

ADD_LIBRARY(alcp_static STATIC
	${ALCP_VRS_SRC}
	)

SET_TARGET_PROPERTIES(alcp_static PROPERTIES OUTPUT_NAME alcp)

# Enable CPUID if enabled and available
IF(ENABLE_AOCL_CPUID)
	MESSAGE(STATUS "Enabling AOCL CPUID Support")
	IF(AOCL_CPUID_INSTALL_DIR)
		MESSAGE(STATUS "AOCL_CPUID_INSTALL_DIR set, overriding fetch path")
	ELSE(AOCL_CPUID_INSTALL_DIR)
		SET(AOCL_CPUID_INSTALL_DIR "${CMAKE_SOURCE_DIR}/external")
		MESSAGE(STATUS "AOCL_CPUID_INSTALL_DIR not set, defaulting to external")
	ENDIF(AOCL_CPUID_INSTALL_DIR)
	IF(EXISTS ${AOCL_CPUID_INSTALL_DIR} AND IS_DIRECTORY ${AOCL_CPUID_INSTALL_DIR})
		TARGET_INCLUDE_DIRECTORIES(alcp PUBLIC ${AOCL_CPUID_INSTALL_DIR}/include)
		IF(MSVC)
			TARGET_LINK_LIBRARIES(alcp PUBLIC ${AOCL_CPUID_INSTALL_DIR}/lib/libaoclutils.lib)
			TARGET_INCLUDE_DIRECTORIES(alcp PUBLIC ${AOCL_CPUID_INSTALL_DIR}/bin)
			TARGET_COMPILE_OPTIONS(alcp PRIVATE "-Wno-microsoft-enum-value")
		ELSE()
			TARGET_LINK_LIBRARIES(alcp PUBLIC ${AOCL_CPUID_INSTALL_DIR}/lib/libaoclutils.so)
		ENDIF()
	ELSE(EXISTS ${AOCL_CPUID_INSTALL_DIR} AND IS_DIRECTORY ${AOCL_CPUID_INSTALL_DIR})
		MESSAGE(FATAL_ERROR "AOCL CPUID fallback error, external directory not found!")
	ENDIF(EXISTS ${AOCL_CPUID_INSTALL_DIR} AND IS_DIRECTORY ${AOCL_CPUID_INSTALL_DIR})
ENDIF(ENABLE_AOCL_CPUID)

IF(WIN32)
	IF(EXISTS ${OPENSSL_INSTALL_DIR}/lib/libcrypto.lib)
		INCLUDE_DIRECTORIES(${OPENSSL_INSTALL_DIR}/include)
		TARGET_LINK_LIBRARIES(alcp PUBLIC ${OPENSSL_INSTALL_DIR}/lib/libcrypto.lib)
	ENDIF()
ENDIF(WIN32)

TARGET_SOURCES(alcp
	PRIVATE
	${ALCP_LIB_SOURCES}
	)

IF(UNIX)
TARGET_SOURCES(alcp_static
	PRIVATE
	${ALCP_LIB_SOURCES}
	)
ENDIF(UNIX)

ADD_SUBDIRECTORY(arch)
ADD_SUBDIRECTORY(capi)
ADD_SUBDIRECTORY(compat)
ADD_SUBDIRECTORY(cipher)
ADD_SUBDIRECTORY(digest)
ADD_SUBDIRECTORY(ref)
ADD_SUBDIRECTORY(utils)
ADD_SUBDIRECTORY(rng)
ADD_SUBDIRECTORY(mac)
ADD_SUBDIRECTORY(ec)
ADD_SUBDIRECTORY(rsa)

ADD_SUBDIRECTORY(tests)

TARGET_INCLUDE_DIRECTORIES(alcp PRIVATE
	${CMAKE_CURRENT_SOURCE_DIR}/include)

TARGET_INCLUDE_DIRECTORIES(alcp_static PRIVATE
	${CMAKE_CURRENT_SOURCE_DIR}/include)

IF(UNIX)
	TARGET_LINK_LIBRARIES(alcp PUBLIC dl)
	TARGET_LINK_LIBRARIES(alcp_static PUBLIC dl)
ENDIF()

# Need to build comapt libs after building the libalcp.so
#${COMPAT_SRCS}
